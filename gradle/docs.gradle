import grails.doc.DocPublisher

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.grails:grails-docs:2.3.6'
    }
}

task sourcesJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier "javadoc"
    from javadoc
}

artifacts {
    archives sourcesJar, javadocJar
}

task gdocs() {
    group 'Documentation'
    description 'Generate Grails Doc style documentation'
    doLast {
        def srcDocs = file("src/docs")
        def outputDir = file("docs/manual")
        def publisher = new DocPublisher(srcDocs, outputDir)
        publisher.with {
            ant = project.ant
            title = 'Grails Gradle Plugin'
            subtitle = 'Grails Gradle Plugin'
            version = project.version
            authors = 'Luke Daley, Graeme Rocher, Peter Ledbrook, John Engelman'
            license = 'The Apache Software License, Version 2.0'
            copyright = ''
            footer = ''
            engineProperties = [:]
        }
        publisher.publish()
    }
}